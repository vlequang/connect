<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:components="com.adobe.sync.components.*" 
				xmlns:qrcode="org.qrcode.*"
				layout="vertical" addedToStage="init(event)">
	
	<mx:Script>
		<![CDATA[
			import com.adobe.sync.events.SyncSwfEvent;
			import mx.graphics.codec.PNGEncoder;
			import mx.utils.Base64Encoder;
			
			[Bindable] private var bitmap:Bitmap;
			[Bindable] private var language:String = Capabilities.language;
			private var dataURI:String;

			/****************************************************
			 * function init
			 * Initialize. If we passed a parameter url=<url>, this value will be used as the QR code.
			 */
			protected function init(event:Event):void
			{
				if(loaderInfo.parameters.url) {
					setQR(loaderInfo.parameters.url);
				}
			}

			/****************************************************
			 * function setQR
			 * Sets the QR code
			 */
			private function setQR(url:String):void {
				// encode QR code image
				qr.encode(url);
				bitmap = new Bitmap(qr.bitmapData);
				
				//	get data-uri of QR code image
				var bytes:ByteArray = new PNGEncoder().encode(qr.bitmapData);
				var base64:Base64Encoder = new Base64Encoder();
				base64.encodeBytes(bytes);
				dataURI = "data:image/png;base64,"+base64;
			}
			
			/****************************************************
			 * function onCaughtUp
			 * Initialize variables when the SyncConnector is ready
			 */
			protected function onCaughtUp(event:SyncSwfEvent):void
			{
				language = syncConnector.language;
				setQR(syncConnector.url);
			}
			
			/****************************************************
			 * function onClick
			 * Open the image in a web browser
			 */
			protected function onClick(event:MouseEvent):void
			{
				navigateToURL(new URLRequest(dataURI));
			}
			
			/****************************************************
			 * function getTip
			 * Show a tooltip depending on the language. Support French and English (default)
			 */
			private function getTip(language:String):String
			{
				switch(language) {
					case "fr":
						return "Cliquez sur l'image pour l'ouvrir dans un navigateur";
						break;
					default:
						return "Click image to open it in a web browser";
				}
			}
			
		]]>
	</mx:Script>
	
	<mx:Image source="{bitmap}" click="onClick(event)" buttonMode="{bitmap!=null}" toolTip="{getTip(language)}"/>
	<components:SyncConnector id="syncConnector" caughtUp="onCaughtUp(event)"/>
	<qrcode:QRCode id="qr"/>
	
</mx:Application>
